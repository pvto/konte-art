DEF BLOCKS 20

fov { CABINET 50 0.5 rx -10 ry -80 y .1 x -.1}
all {
  city { z .1}
}



rule city {
  (BLOCKS)*{x 1/BLOCKS layer -1}
    (BLOCKS)*{z 1/BLOCKS layer -1}
    {
      create_building{x -.5 z -.25 rx 90 scale 1/BLOCKS}
      SQUARE{layer -2 x -.5 z -.25 rx 90 z -.5/BLOCKS scale 4/BLOCKS}
    }
}
create_building 3{create_building{col0 (col0+.1)}}
create_building {building{shading shd sx .5 sy .5 sz .8}}
building {
  block{}
  building_{z 1 layer .1}
}
building_ .4 {
  2*{x .5} 2*{y .5}
  building { s .5 x -.5 y -.5 z -.5}
}
building_ 1{}
building_ 1{building{}}

block {
  BOX{s .8 shading -1 y .1 A -.7}
  cub{PUSH frame {BO=0.4}}
}


cub{
  PEEK{z -.5}
  PEEK{z .5 ry 180}
  PEEK{x -.5 ry -90}
  PEEK{x .5 ry 90}
  PEEK{y -.5 rx 90}
  PEEK{y .5 rx -90}
}


shading shd {
    point(0.0) { A 1        RGB 0.6 0 0.2 }
    point(.25) { A 1        RGB 0.918 0.918 0 }
    point(.5)  { A 1        RGB 0.8 0.8 0.8 }
    point(.75) { A 1        RGB 0.522 0.718 0.886 }
    point(1.0) { A 1        RGB 0.420 0.247 0 }
}


DEF BO 0.25

path frame {
    moveto(-.5,-.5,.0)
    lineto(-.5,.5,.0)
    lineto(.5,.5,.0)
    lineto(.5,-.5,.0)
    lineto(-.5,-.5,.0)
    close
    moveto(-BO,-BO,.0)
    lineto(BO,-BO,.0)
    lineto(BO,BO,.0)
    lineto(-BO,BO,.0)
    lineto(-BO,-BO,.0)
    close
}
